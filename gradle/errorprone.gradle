buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        //taken from https://github.com/tbroyer/gradle-errorprone-plugin
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:1.1.1'
    }
}

apply plugin: "net.ltgt.errorprone"

dependencies {
    //annotationProcessor "com.uber.nullaway:nullaway:0.6.4"

    //taken from https://github.com/google/error-prone/releases
    errorprone 'com.google.errorprone:error_prone_core:2.3.4'

    if (!JavaVersion.current().java9Compatible) {
        //this is required for Java8, so error-prone can run.
        errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")
    }
}

afterEvaluate {
    tasks.withType(JavaCompile).configureEach {
        options.errorprone.errorproneArgs.add('-Xep:MixedMutabilityReturnType:OFF')
        options.errorprone.errorproneArgs.add('-Xep:UnnecessaryLambda:OFF')
        options.errorprone.errorproneArgs.add('-Xep:UnnecessaryAnonymousClass:OFF')

        if (name.toLowerCase(Locale.ROOT).contains('test')) {
            options.errorprone.errorproneArgs.add('-Xep:UndefinedEquals:OFF')
        }
//            task.options.errorprone {
//                check("NullAway", CheckSeverity.ERROR)
//                option("NullAway:AnnotatedPackages", "com.uber")
//            }
    }
}