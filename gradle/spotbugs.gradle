import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.3"
    }
}

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion '3.1.7'
    ignoreFailures = false

    effort = "min"
    reportLevel = "high"
    showProgress = true
}

dependencies {
    //spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
    //spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.3.sb'
}

task spotbugsMain(type: SpotBugsTask, dependsOn: 'assembleDebug', group: 'verification') {
    classes = fileTree(
            dir: "${project.buildDir}/intermediates/javac/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       'android/**/*.class',
                       '**/*$ViewInjector*.*',
                       '**/*$ViewBinder*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*'])

    source = fileTree('src/main/java')

    // Only one report format is supported. Html is easier to read, so let's use that
    // (xml is the one that's enabled by default).
    reports {
        xml.enabled = false
        html.enabled = true
        html.destination = file("$project.buildDir/reports/spotbugs/spotbugs.html")
    }

    classpath = files()
    pluginClasspath = project.configurations.spotbugsPlugins
    spotbugsClasspath = buildscript.configurations.classpath

    excludeFilter = file("$rootDir/configs/findbugs-exclude.xml")
}