
apply from: "${rootDir}/gradle/errorprone.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/pmd.gradle"

android {
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion sdkCompileVersion
    buildToolsVersion androidBuildTools

    defaultConfig {
        minSdkVersion sdkMinimumVersion
        targetSdkVersion sdkTargetVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig file("${rootDir}/configs/lint.xml")
    }

    aaptOptions {
        //does not compress any asset
        noCompress ''
    }
}

apply from: "${rootDir}/gradle/android_unit_test.gradle"

afterEvaluate {
    tasks.withType(JavaCompile) { JavaCompile task ->
        //fail on all warnings
        task.options.compilerArgs << '-Xlint:all' << '-Werror'
        //we need to support old devices, so we can trust Java's detection here.
        task.options.compilerArgs << '-Xlint:-deprecation'
        if (task.name.contains('UnitTest')) {
            //for tests, we do not check other stuff too
            task.options.compilerArgs << '-Xlint:-rawtypes' << '-Xlint:-cast' << '-Xlint:-unchecked' << '-Xlint:-classfile'
        }
    }
}