import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

android {
    testOptions {
        unitTests {
            includeAndroidResources = true

            all {
                int maxTestForks = Integer.parseInt(System.getProperty('maxTestForks', project.path == ':app' ? '4' : '1'))
                maxParallelForks Math.max(1, (int) Math.min(maxTestForks, (int) (Runtime.getRuntime().availableProcessors() / 2.0)))

                forkEvery Integer.parseInt(System.getProperty('TEST_FORK_EVERY', '1000'))

                testLogging {
                    if (isCi) {
                        events TestLogEvent.FAILED, TestLogEvent.SKIPPED, TestLogEvent.STARTED, TestLogEvent.PASSED
                    } else {
                        events TestLogEvent.FAILED, TestLogEvent.SKIPPED
                    }

                    exceptionFormat TestExceptionFormat.FULL

                    showCauses true
                    showExceptions true
                    showStackTraces true
                }

                systemProperties['TestsGroupingFilter_TEST_GROUP_TO_EXECUTE_SYSTEM_PROPERTY_KEY'] = System.getenv().getOrDefault('TEST_GROUP_INDEX', 0)
                systemProperties['TestsGroupingFilter_TEST_GROUPS_COUNT_SYSTEM_PROPERTY_KEY'] = System.getenv().getOrDefault('TEST_GROUPS_COUNT', 1)

                systemProperties['java.awt.headless'] = true
                systemProperties['robolectric.logging'] = 'stdout'
                //disabling since there is just too much logging happening in the Robolectric layer.
                systemProperties['robolectric.logging.enabled'] = false

                //ensuring Robolectric downloads its resources from maven
                systemProperties['robolectric.dependency.repo.id'] = 'sonatype'
                systemProperties['robolectric.dependency.repo.url'] = 'https://oss.sonatype.org/content/groups/public/'

                if (project.hasProperty('excludeTestClasses')) {
                    exclude project.property('excludeTestClasses')
                }

                // set heap size for the test JVM(s)
                // NOTE: This is needed since Gradle does not propagate the heap-size restrictions from gradle.properties file.
                minHeapSize = "128m"
                maxHeapSize = "1900m"
            }
        }
    }
}

apply from: "${rootDir}/gradle/jacoco.gradle"
