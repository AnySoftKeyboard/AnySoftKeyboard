apply plugin: 'com.android.library'

apply plugin: 'net.evendanan.versiongenerator'

def generators = [
        new net.evendanan.versiongenerator.generators.EnvBuildVersionGenerator.CircleCi(0, 0),
        new net.evendanan.versiongenerator.generators.GitBuildVersionGenerator(0, 0),
        new net.evendanan.versiongenerator.generators.StaticVersionGenerator()
]

def versionData = versionGenerator.generateVersion(1, 11, 0, generators)
rootProject.version = versionData.versionName
rootProject.group = 'com.anysoftkeyboard.api'
group = 'com.anysoftkeyboard.api'
version = versionData.versionName

rootProject.file('.generated_pack_version').write(versionData.versionName)

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 28
        versionCode versionData.versionCode
        versionName versionData.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    testOptions.unitTests.includeAndroidResources true
}

apply plugin: 'com.novoda.bintray-release'

publish {
    userOrg = 'anysoftkeyboard'
    repoName = 'API'
    groupId = group
    artifactId = 'api'
    publishVersion = version
    desc = "API library for AnySoftKeyboard add-ons"
    website = "http://anysoftkeyboard.github.io"
    licences = ['Apache-2.0']
    issueTracker = 'https://github.com/AnySoftKeyboard/AnySoftKeyboard-API/issues'
    autoPublish = true

    bintrayUser = 'menny'
    bintrayKey = System.getenv('BINTRAY_API_KEY')
}

dependencies {
    testImplementation 'org.robolectric:robolectric:4.0.2'
    testImplementation 'junit:junit:4.12'
}