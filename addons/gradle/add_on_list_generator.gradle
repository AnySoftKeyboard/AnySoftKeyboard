tasks.register("generatePacksMarkDown") {
    group "AnySoftKeyboard"
    description "Writing a MarkDown-friendly list of ${project.name} to PACKS.md."

    doLast {
        def outputFile = project.file('PACKS.md')
        outputFile.withWriter { BufferedWriter writer ->
            writer.writeLine('# Packs in this repository')
            subprojects.each {
                if (it.plugins.hasPlugin('com.android.application')) {
                    def image = "<img src='${it.rootDir.relativePath(it.projectDir)}/flag/{{IMAGE_FILENAME}}' height='16'>&nbsp;"
                    if (it.file("flag/flag.png").exists()) {
                        image = image.replace("{{IMAGE_FILENAME}}", "flag.png")
                    } else if (it.file("flag/flag.svg").exists()) {
                        image = image.replace("{{IMAGE_FILENAME}}", "flag.svg?sanitize=1")
                    } else {
                        image = ""
                    }
                    def packReadMeDetails = "1. ${image}${it.parent.name.capitalize()}: [Source](${it.rootDir.relativePath(it.parent.projectDir)})"

                    if (it.ext.shouldBePublished) {
                        packReadMeDetails += " â€¢ [Play Store](https://play.google.com/store/apps/details?id=${it.android.defaultConfig.applicationId})"
                    }

                    writer.writeLine(packReadMeDetails)
                }
            }
        }
    }
}
